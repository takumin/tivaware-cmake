/******************************************************************************
 *
 * linker.ld - GNU Linker configuration file.
 *
 *****************************************************************************/

MEMORY
{
    FLASH (rx) : ORIGIN = 0x00000000, LENGTH = 0x00040000
    SRAM (rwx) : ORIGIN = 0x20000000, LENGTH = 0x00008000
}

ENTRY(ResetISR)

SECTIONS
{
    .text :
    {
        _text = .;
        KEEP(*(.isr_vector))
        *(.text*)
        *(.rodata*)
        _etext = .;
    } > FLASH

    .ARM.exidx :
    {
      *(.ARM.exidx*)
      *(.gnu.linkonce.armexidx.*)
    } > FLASH

    _armexidx = .;

    .ARM.extab :
    {
      *(.ARM.extab*)
      *(.gnu.linkonce.armextab.*)
    } > FLASH

    _armextab = .;

    .data : AT( _armextab )
    {
        _data = .;
        _ldata = LOADADDR (.data);
        *(vtable)
        *(.data*)
        _edata = .;
    } > SRAM

    .bss :
    {
        _bss = .;
        *(.bss*)
        *(COMMON)
        _ebss = .;
    } > SRAM

    /DISCARD/ :
    {
      libc.a ( * )
      libg.a ( * )
      libgcc.a ( * )
      libm.a ( * )
      libstdc++.a ( * )
      libsupc++.a ( * )
      librdimon.a ( * )
    }
}
